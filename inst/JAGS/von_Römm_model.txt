model{
# model for recoveries with known number of ringed
for (i in 1:npop){
  for(k in 1:ndest){
    pad[i, k] <- m[i,k]*rad[k]
    pjuv[i,k] <- m[i,k]*rjuv[k]
  }
  pad[i, (ndest+1)] <- 1-sum(pad[i,1:ndest])
  pjuv[i, (ndest+1)] <- 1-sum(pjuv[i, 1:ndest])
}

# likelihood
for(i in 1:npop){
  recmatad[i,1:(ndest+1)]~dmulti(pad[i,1:(ndest+1)], nringedad[i])
  recmatjuv[i,1:(ndest+1)]~dmulti(pjuv[i,1:(ndest+1)], nringedjuv[i])
}

# model for recoveries with unknown ringed
for(i in 1:npop){
  for(k in 1:ndest){
    Epad[i,k] <- m[i,k]*rad[k]/(m[i,1]*rad[1]+m[i,2]*rad[2]+m[i,3]*rad[3]+m[i,4]*rad[4])
    Epjuv[i,k] <- m[i,k]*rjuv[k]/(m[i,1]*rjuv[1]+m[i,2]*rjuv[2]+m[i,3]*rjuv[3]+m[i,4]*rjuv[4])
  }
  recmatruad[i,1:ndest] ~ dmulti(Epad[i,1:ndest], Rad[i])
  recmatrujuv[i,1:ndest] ~ dmulti(Epjuv[i,1:ndest], Rjuv[i])
}

# priors
rad[1] ~ dunif(0, 1)  # W-Africa
rad[2] ~ dunif(0, 1)  # C-Africa
rad[3] <- rad[2]       # E-Africa
rad[4] ~ dunif(0, 1)  # S-Africa
rjuv[1] ~ dunif(0, 1)  # W-Africa
rjuv[2] ~ dunif(0, 1)  # C-Africa
rjuv[3] <- rjuv[2]       # E-Africa
rjuv[4] ~ dunif(0, 1)  # S-Africa

# Parasite model
# within Africa
for(j in 1:nparasites){
  for(k in 1 : ndest) {
    infw[k,j]~dbin(pwi[k,j], nwinter[k])
  }
}

# breeding population
for(i in 1:3){
 for(j in 1:nparasites){
   infsommer[i,j]~dbin(pinf[i,j], nsommer[i])
 }
}

for(i in 1:3){
  for(j in 1:nparasites){
    for(k in 1:ndest){
      pwim[i,j,k] <- pwi[k,j]*a*m[poppar[i],k]
    }
    pinf[i,j] <- sum(pwim[i,j,1:ndest])
  }
}

# initials
a ~ dnorm(0,0.04)I(0,)
for(k in 1:ndest){
  for(j in 1:nparasites){
    pwi[k,j] ~ dunif(0,1)
  }
}

# model for isotopes
for(i in 1 : N) {
  y[i,1:2] ~ dmnorm(mu[WiDest[i],1:2], Sigma.inv[WiDest[i],,])
  WiDest[i] ~ dcat(miso[pop[i],1:ndestiso]) # miso: proportion of birds from i in k
}

for(i in 1:npop){
  miso[i,1] <- sum(m[i,1:3])
  miso[i,2] <- m[i,4]
}

for(k in 1:ndestiso){
  mu[k,2] ~ dnorm(0, 1)  # mean nitrogen stable isotope values for each area k
}
# mean carbon stable isotope values for S-Africa and WCE-Africa
mu[2,1] ~ dnorm(0.97, 200)  # for S-Africa
theta1 ~ dnorm(0, 1)I(,0)
mu[1,1] <- mu[2,1] + theta1 # for WCE-Africa

for(k in 1:ndestiso){
  Sigma.inv[k,1:2, 1:2] <- inverse(Sigma[k, 1:2, 1:2])
  sigma.c[k] ~ dt(0,1,2)I(0,) # sd in d13c
  sigma.n[k] ~ dt(0,1,2)I(0,) # sd in d15n
  Sigma[k,1,1] <- pow(sigma.c[k],2)
  Sigma[k,2,2] <- pow(sigma.n[k],2)
  Sigma[k,1,2] <- rho[k]*sigma.c[k]*sigma.n[k]
  Sigma[k,2,1] <- Sigma[k,1,2]
}

rho0[1] ~ dbeta(2,4)
rho[1] <- 2*(rho0[1]-0.5)
rho0[2] ~ dbeta(10,4)     # for S-Africa
rho[2] <- 2*(rho0[2]-0.5)

for(i in 1:npop){
  for(k in 1:ndest){
    m0[i,k]~dbeta(1,1)
    m[i,k] <- m0[i,k]/sum(m0[i,1:ndest])
  } #k
}#i
}
